options
{
    LOOKAHEAD=1;
}

PARSER_BEGIN(Jmm)
public class Jmm { public static void main(String args[]) throws ParseException { } }

PARSER_END(Jmm)

JAVACODE
void error_skipto(int kind) {
  ParseException e = generateParseException();  // generate the exception object
  System.out.println("While Error: " + e.toString());  // print the error message
  Token t;
  Token next = getToken(1);
  // consume tokens all the way up to a token of "kind" - use a do-while loop
  // rather than a while because the current token is the one immediately before
  // the erroneous token (in our case the token immediately before what should
  // have been "if"/"while".
  while (next.kind != kind && next.kind != 0){
    t = getNextToken();
    next = getToken(1);
//    System.out.print(t);
//    System.out.println(getToken(1));
  }
}

SKIP :
{
      " "
    | "\t"
    | "\r"
    | "\n"
}

/////////////////////////////////////////////////////////////////////////////
// From the JavaCC FAQ https://javacc.github.io/javacc/faq.html#question-3.14
/////////////////////////////////////////////////////////////////////////////

// when /* is seen in the DEFAULT state, skip it and switch to the IN_COMMENT state
MORE : {
  "/*": IN_COMMENT
  | "//": IN_SINGLE_LINE_COMMENT
}

// when any other character is seen in the IN_COMMENT state, skip it.
< IN_COMMENT > MORE : {
  < ~[] >
}

// when */ is seen in the IN_COMMENT state, skip it and switch back to the DEFAULT state
< IN_COMMENT > SKIP : {
  "*/": DEFAULT
}

< IN_SINGLE_LINE_COMMENT > SPECIAL_TOKEN : {
  < SINGLE_LINE_COMMENT: "\n"|"\r"|"\r\n" > : DEFAULT
}

< IN_SINGLE_LINE_COMMENT > MORE : {
  < ~[] >
}

TOKEN :
{

    <IntegerLiteral: <Digit> (<Digit>)*>
    | <Digit: ["0" - "9"] >

    | <OpenBlock: "{" >
    | <CloseBlock: "}" >
    | <OpenParenthesis: "(" >
    | <CloseParenthesis: ")" >
    | <OpenArray: "[" >
    | <CloseArray: "]" >
    | <Semicolon: ";" >
    | <Comma: "," >
    | <Equals: "=" >
    | <Dot: "." >

    | <Class: "class" >
    | <Import: "import" >
    | <If: "if" >
    | <Else: "else" >
    | <While: "while" >
    | <TypeInt: "int" >
    | <TypeBoolean: "boolean" >
    | <TypeString: "String" >
    | <Extends: "extends" >
    | <Public: "public" >
    | <Static: "static" >
    | <Void: "void" >
    | <Main: "main" >
    | <Return: "return" >
    | <Length: "length" >
    | <True: "true" >
    | <False: "false" >
    | <This: "this" >
    | <New: "new" >

    | <AndOp: "&&" >
    | <LessThanOp: "<" >
    | <AddOp: "+" >
    | <SubOp: "-" >
    | <MultOp: "*" >
    | <NotOp: "!" >
    | <Print: "System.out.println">

    | <Identifier: <Letter> (<Letter> | <Digit> | "_" | "$")* >
    | <Letter: (<LowerCase> | <UpperCase>) >
    | <LowerCase: ["a"-"z"] >
    | <UpperCase: ["A"-"Z"] >
}

SimpleNode Program(): {} {
    (ImportDeclaration())*
    ClassDeclaration()
    <EOF>
    { return jjtThis; }
}

void ImportDeclaration(): { Token t_name; } {
    <Import> t_name = <Identifier> { jjtThis.put("name", t_name.image); } (<Dot> <Identifier>)* <Semicolon>
}

void ClassDeclaration(): { Token t_name; } {
    <Class> t_name = <Identifier> { jjtThis.put("name", t_name.image); } [ClassInheritance()]
    <OpenBlock>
        (VarDeclaration())*
        (MethodDeclaration())*
    <CloseBlock>
}

void ClassInheritance(): { Token t_type; } {
    <Extends> t_type = <Identifier> { jjtThis.put("type", t_type.image); }
}

void VarDeclaration(): { Token t_name; } {
    Type() t_name = <Identifier> { jjtThis.put("name", t_name.image); } <Semicolon>
}

void MethodDeclaration(): { Token t_name; } {
    <Public>
    (
    Type() t_name = <Identifier> { jjtThis.put("name", t_name.image); } // Ha forma de substituir o node Type por um novo chamado ReturnType mas c o valor dele??
    <OpenParenthesis>
        Parameters()
    <CloseParenthesis>
    <OpenBlock>
        (LOOKAHEAD(2) VarDeclaration() | Statement())* // Allowed for now
        Return()
    <CloseBlock>
    |
    <Static> <Void> t_name = <Main> { jjtThis.put("name", t_name.image); } <OpenParenthesis> <TypeString> <OpenArray> <CloseArray>
    <Identifier> <CloseParenthesis> <OpenBlock>
        (LOOKAHEAD(2) VarDeclaration() | Statement())* // Allowed for now
    <CloseBlock>
    )
}

void Parameters() #void: {} {
    [Parameter() (<Comma> Parameter())* ]
}

void Parameter(): { Token t_name; } {
    Type() t_name = <Identifier> { jjtThis.put("name", t_name.image); }
}

void Type(): { Token t_type; } {
    ( (t_type = <TypeInt> [<OpenArray> <CloseArray>])
    | t_type = <TypeBoolean>
    | t_type = <Identifier>
    ) { jjtThis.put("type", t_type.image); }
}

void Statement() #void: {} {
    (<OpenBlock> (Statement())* <CloseBlock> )
    | (<If> <OpenParenthesis> Expression() #IfCondition <CloseParenthesis> Statement() <Else> Statement() #Else) #IfStatement
    | WhileStatement()
    | (<Print> <OpenParenthesis> Expression() <CloseParenthesis> <Semicolon> ) #PrintStatement
    | (Expression() [Assignment()] <Semicolon>)
}

void WhileStatement(): {} {
    <While> <OpenParenthesis>
    try{
        (Expression() #WhileCondition <CloseParenthesis>)
    }
    catch(ParseException e){
        error_skipto(OpenBlock);
    }

    Statement() #WhileBody
}

void Assignment() #void: {} {
    <Equals> Expression() #Assignment(2)
}


void Return(): {} {
    <Return> Expression() <Semicolon>
}

void Expression() #void: {} {
    And()
}

void And() #void: {} {
    LessThan() (<AndOp> LessThan() #AndExpression(2) )*
}

void LessThan() #void: {} {
    Add() (<LessThanOp> Add() #LessThanExpression(2) )*
}

void Add() #void: {} {
    Sub() (<AddOp> Sub() #AddExpression(2) )*
}

void Sub() #void: {} {
    Mult() (<SubOp> Mult()  #SubExpression(2) )*
}

void Mult() #void: {} {
    Not() (<MultOp> Not()  #MultExpression(2) )*
}

void Not() #void: {} {
    (<NotOp> Not() #NotExpression) | Terminations()
}

void Terminations() #void: {} {
    Terminal() (Array() | DotStatement())* // o intellij est√° a queixar se disto mas n percebo pq
}

void Array() #void: {} {
    <OpenArray> Expression() <CloseArray> #ArrayExpression(2)
}

void DotStatement() #CallExpression: { Token t_name; } {
    <Dot>
    (
    (t_name = <Length> { jjtThis.put("name", t_name.image); })
    | (t_name = <Identifier> { jjtThis.put("name", t_name.image); } <OpenParenthesis> [Expression() (<Comma> Expression())*] <CloseParenthesis> )
    )
}

void Terminal() #void: { Token t_value; } {
    ( t_value = <IntegerLiteral> { jjtThis.put("value", t_value.image); } ) #IntegerLiteral
    | ( t_value = <True> { jjtThis.put("value", t_value.image); } ) #Bool
    | ( t_value = <False> { jjtThis.put("value", t_value.image); } ) #Bool
    | ( t_value = <Identifier> { jjtThis.put("value", t_value.image); } ) #Identifier
    | ( <This> ) #This
    | (<OpenParenthesis> Expression() <CloseParenthesis> ) #ParenthesisExpression
    | New()
}

void New() #void: { Token t_type; } {
    <New>
    (
    ( <TypeInt> <OpenArray> Expression() <CloseArray> ) #NewIntArray
    | ( t_type = <Identifier> { jjtThis.put("type", t_type.image); } <OpenParenthesis> <CloseParenthesis> ) #NewClass
    )
}